apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        versionCode 19
        versionName '0.6.1.2'
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true

        // For Espresso
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    dexOptions {
        javaMaxHeapSize '2g'
        jumboMode = true
    }
}

def supportVersion = '28.0.0'

configurations.all {
    resolutionStrategy {
        force "com.android.support:design:$supportVersion"
        force "com.android.support:support-annotations:$supportVersion"
    }
}

dependencies {
    // Local dependencies
    implementation(project(':core')) {
        // Replaced with Android-specific JavaMail
        exclude group: 'com.sun.mail'
        exclude group: 'com.sun.activation'
        // BouncyCastle is replaced with SpongyCastle
        exclude group: 'org.bouncycastle'
    }
    implementation project(':crypto')
    implementation fileTree(dir: 'libs', include: '*.jar')

    // Android Support Repository dependencies
    //noinspection GradleCompatible
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Remote dependencies
    implementation 'com.sun.mail:android-mail:1.6.5'
    implementation 'com.sun.mail:android-activation:1.6.5'
    implementation "net.i2p:router:$rootProject.i2pVersion"
    implementation "net.i2p.android:client:$rootProject.i2pVersion@aar"
    implementation 'net.i2p.android:helper:0.9.5@aar'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.10.1'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.androidplot:androidplot-core:1.5.6'
    implementation 'com.inkapplications.viewpageindicator:library:2.4.4'
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.mikepenz:iconics-core:2.7.2@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation('com.mikepenz:materialdrawer:5.4.0@aar') {
        transitive = true
    }
    implementation 'org.sufficientlysecure:html-textview:3.1'

    // Testing-only dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
}

project.ext.i2pbase = '../../i2p.i2p'
Properties props = new Properties()
def propFile = new File(project(':android').projectDir, 'local.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('i2psrc')) {
        i2pbase = props['i2psrc']
    } else {
        println 'local.properties found but some entries are missing'
    }
} else {
    println 'local.properties not found'
}

task certificatesZip(type: Zip) {
    archiveFileName = 'certificates_zip'
    into('reseed') {
        from files('' + i2pbase + '/installer/resources/certificates/reseed')
    }
    into('ssl') {
        from files('' + i2pbase + '/installer/resources/certificates/ssl')
    }
}
task copyI2PResources(type: Copy) {
    // Force this to always run: Copy only detects source changes, not if missing in destination
    outputs.upToDateWhen { false }
    into 'src/main/res/raw'
    from certificatesZip
}
task cleanI2PResources(type: Delete) {
    delete file('src/main/res/raw/certificates_zip')
}
preBuild.dependsOn copyI2PResources
clean.dependsOn cleanI2PResources

apply from: "${project.rootDir}/gradle/signing.gradle"
